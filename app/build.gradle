apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

ext.versionMajor = 1
ext.versionMinor = 1

android {
    compileSdkVersion 29

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
    
    defaultConfig {
        applicationId "fi.oulu.ubicomp.extrema"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode computeVersionCode()
        versionName computeVersionName()

        setProperty("archivesBaseName","extrema-cerh") //auto renames the signed .apk to be extrema-cerh.apk

        kapt {
            generateStubs = true
            mapDiagnosticLocations = true
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
            }
        }
    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/proguard/androidx-annotations.pro'
    }
}

def computeVersionCode() {
    return Integer.valueOf(System.env.BUILD_NUMBER ?: 1)
}

def computeVersionName() {
    return String.format('%d.%d.%d', versionMajor, versionMinor, computeVersionCode())
}

dependencies {
    def room_version = "2.1.0"
    def anko_version = "0.10.8"
    def work_version = "2.1.0"
    def gson_version = "2.8.5"
    def volley_version = "1.1.1"

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta2'
    implementation 'com.google.android.material:material:1.1.0-alpha09'

    implementation "org.jetbrains.anko:anko:$anko_version"

    implementation "androidx.room:room-common:$room_version"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"

    implementation "androidx.work:work-runtime-ktx:$work_version"
    implementation "com.google.code.gson:gson:$gson_version"
    implementation "com.android.volley:volley:$volley_version"

    //Bluetooth libraries for RuuviTag support and Beacon scanning
    implementation 'org.altbeacon:android-beacon-library:2.16.2'
    implementation 'com.neovisionaries:nv-bluetooth:1.8'

    implementation 'com.koushikdutta.ion:ion:2.2.1'
}
